# Copyright 2021 Eric Fedosejevs
#

include(${UTILOGENY_DIR}/cmake/find_install_package.cmake)
include(${UTILOGENY_DIR}/cmake/target_deploy_lib.cmake)

option(ENABLE_SDL2 "Enable SDL2" ON)
option(ENABLE_SDL2_MIXER "Enable audio support using SDL2_mixer" ON)
option(ENABLE_SDL2_IMAGE "Enable image support using SDL2_image" ON)

macro(check_sdl_settings)
	if(ENABLE_SDL2_MIXER AND NOT ENABLE_SDL2)
		message(FATAL_ERROR "Cannot enable SDL2 mixer without enabling SDL2!")
	endif()

	if(ENABLE_SDL2_IMAGE AND NOT ENABLE_SDL2)
		message(FATAL_ERROR "Cannot enable SDL2 image without enabling SDL2!")
	endif()
endmacro()

function(target_include_sdl2 target)
	check_sdl_settings()
	set(SDL2_BUILDING_LIBRARY 1) # Omit SDL main()
	if(ENABLE_SDL2)
		find_install_package(PACKAGE_NAME "SDL2" REQUIRED_TARGETS "SDL2::SDL2")
		target_include_lib(LINK_TARGET ${target} PACKAGE_NAME "SDL2")
	endif()
	if(ENABLE_SDL2_MIXER)
		find_install_package(PACKAGE_NAME "sdl2-mixer" VCPKG_NAME "sdl2-mixer" REQUIRED_TARGETS "SDL2::SDL2_mixer" INCLUDES_NOT_REQUIRED)		
		target_include_lib(LINK_TARGET ${target} PACKAGE_NAME "sdl2-mixer")
	endif()
	if(ENABLE_SDL2_IMAGE)
		find_install_package(PACKAGE_NAME "sdl2-image" VCPKG_NAME "sdl2-image" REQUIRED_TARGETS "SDL2::SDL2_image" INCLUDES_NOT_REQUIRED)
		target_include_lib(LINK_TARGET ${target} PACKAGE_NAME "sdl2-image")
	endif()
endfunction()

function(target_link_sdl2 target)
	check_sdl_settings()
	target_include_sdl2(${target})
	if(ENABLE_SDL2)
		target_link_deploy_lib(LINK_TARGET ${target} PACKAGE_NAME "SDL2")
	endif()
	if(ENABLE_SDL2_MIXER)
		target_link_deploy_lib(LINK_TARGET ${target} PACKAGE_NAME "sdl2-mixer")
	endif()	
	if(ENABLE_SDL2_IMAGE)
		target_link_deploy_lib(LINK_TARGET ${target} PACKAGE_NAME "sdl2-image")
	endif()
endfunction()